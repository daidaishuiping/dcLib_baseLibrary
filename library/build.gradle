plugins {
    alias(libs.plugins.androidLibrary)
    id 'maven-publish'   //添加maven publish插件
}

android {
    namespace 'com.dclib.dclib.baselibrary'
    compileSdk 34

    defaultConfig {
        minSdk 21

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding = true
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.android.daidaishuiping' //组织或公司标识符
                artifactId = 'dcLib_baseLibrary'  //具体的库名称
                version = '1.0.0'
                artifact(bundleReleaseAar)

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.withType(ModuleDependency) { ModuleDependency dp ->
                        if (dp.version != "unspecified") { // 过滤项目内library引用
                            if ("com.android.daidaishuiping" != dp.group) {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', dp.group)
                                dependencyNode.appendNode('artifactId', dp.name)
                                dependencyNode.appendNode('version', dp.version)
                                // for exclusions
                                if (dp.excludeRules.size() > 0) {
                                    def exclusions = dependencyNode.appendNode('exclusions')
                                    dp.excludeRules.each { ExcludeRule ex ->
                                        def exclusion = exclusions.appendNode('exclusion')
                                        exclusion.appendNode('groupId', ex.group)
                                        exclusion.appendNode('artifactId', ex.module)
                                    }
                                }
                            }

                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

    //AndroidUtilCode工具 https://github.com/Blankj/AndroidUtilCode/blob/master/lib/utilcode/README-CN.md
    implementation libs.utilcodex
    //gson https://github.com/google/gson
    implementation libs.gson
    //EventBus https://github.com/greenrobot/EventBus
    api libs.eventbus
    //ShapeView https://github.com/getActivity/ShapeView
    implementation libs.shapeView
    //圆形图片 https://github.com/hdodenhof/CircleImageView
    implementation libs.circleimageview
    //圆角图片 https://github.com/vinc3m1/RoundedImageView
    implementation libs.roundedimageview
    //glide https://github.com/daidaishuiping/dcLib_glide
    implementation libs.dcLibGlide
}